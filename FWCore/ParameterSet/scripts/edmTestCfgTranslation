#!/bin/tcsh 
eval `scramv1 ru -csh`

if($#argv != 1) then
  echo "Please give a target file"
  exit 1
endif

if(! -e $1) then
  echo "Cannot find " $1
  exit 1
endif

set psetDir=$CMSSW_RELEASE_BASE/src/FWCore/ParameterSet/python
#set psetDir=FWCore/ParameterSet/python
set pyFile=`python $psetDir/cfgName2py.py $1`


if(! -e $pyFile) then
  python $psetDir/cfg2py.py $1 >! $pyFile

  if(! -e $pyFile) then
    echo "Something went wrong.  Did not create " $pyFile
    exit 1
  else
    python $psetDir/comments.py $1
  endif
endif


if($1:e == 'cfg') then
  edmParameterSetDump $1 >! dumpcfgpset.txt
  edmParameterSetDump $pyFile >! dumppypset.txt

  # check if it's zero length
  if(-s dumppypset.txt) then
    echo "Comparing the ParameterSets for cfg and py:"
    diff dumpcfgpset.txt dumppypset.txt
  endif
endif


